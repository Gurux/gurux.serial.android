plugins {
    alias(libs.plugins.androidLibrary)
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'gurux.serial'
    compileSdk 34

    defaultConfig {
        minSdk 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation libs.guruxCommonAndroid
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "org.gurux"
                artifactId = "gurux.serial.android"
                version = "2.0.10"

                signing {
                    sign publishing.publications.release
                }

                pom {
                    name = 'gurux.serial.android'
                    description = 'gurux.serial.android package implements serial port for android OS. Purpose of Gurux Device Framework is help you to read your devices, meters and sensors easier'
                    url = 'https://www.gurux.fi'
                    licenses {
                        license {
                            name = 'GNU General Public License, version 2'
                            url = 'http://www.gnu.org/licenses/gpl-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'gurux'
                            name = 'Gurux ltd'
                            email = 'gurux@gurux.fi'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/gurux/gurux.serial.android.git'
                        developerConnection = 'scm:git:https://github.com/gurux/gurux.serial.android.git'
                        url = 'https://github.com/gurux/gurux.serial.android'
                    }
                }
                boolean isEnabled = project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')
                repositories {
                    maven {
                        name = "Maven central"
                        url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
                        if (isEnabled){
                            credentials{
                                username = sonatypeUsername
                                password = sonatypePassword
                            }
                        }
                    }
                }
            }
        }
    }
}